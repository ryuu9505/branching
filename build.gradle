plugins {
    id 'java'
    id 'org.springframework.boot' version '3.3.2'
    id 'io.spring.dependency-management' version '1.1.6'
    id 'checkstyle'
    id 'pmd'
}

group = 'com.xiyoon'
version = '0.0.1-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    checkstyle 'com.puppycrawl.tools:checkstyle:10.9.2'
}

tasks.named('test') {
    useJUnitPlatform()
}

checkstyle {
    toolVersion = '8.37' // 사용할 Checkstyle 버전을 지정합니다.
    config = resources.text.fromFile('config/checkstyle/checkstyle.xml') // Checkstyle 설정 파일 경로를 지정합니다.
}

tasks.withType(Checkstyle) {
    reports {
        xml.required.set(false)
        html.required.set(true)
        html.outputLocation.set(file("${buildDir}/reports/checkstyle/checkstyle.html"))
    }
}

pmd {
    toolVersion = '6.31.0' // 사용할 PMD 버전을 지정합니다.
    ruleSetFiles = files('config/pmd/ruleset.xml') // PMD 설정 파일 경로를 지정합니다.
    ruleSets = [] // 기본 설정 비활성화
}

tasks.withType(Pmd) {
    reports {
        xml.required.set(false)
        html.required.set(true)
        html.outputLocation.set(file("${buildDir}/reports/pmd/pmd.html"))
    }
}